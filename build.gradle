buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'idea'
    id 'net.neoforged.moddev.legacyforge' version '2.0.119'
    id 'com.diffplug.spotless' version '7.2.1'
}

apply from: 'gradle/scripts/helpers.gradle'


// Early Assertions
assertProperty 'mod_version'
assertProperty 'mod_package'
assertProperty 'mod_id'
assertProperty 'mod_name'
assertProperty 'mod_name_suffix'

base {
    version = propertyString('mod_version')
    group = propertyString('mod_package')
    archivesName = propertyString('mod_name', 'mod_name_suffix')
}


java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(java_version))
    }
}

legacyForge {
    version = project.minecraft_version + '-' + project.forge_version

    runs {
        configureEach {
            gameDirectory = project.file('run')

            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'forge.logging.console.level', 'INFO'
        }

        client {
            client()
            gameDirectory = project.file('run/client')

            systemProperty 'mixin.env.remapRefMap', 'true'
            systemProperty 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            server()
            gameDirectory = project.file('run/server')

            systemProperty 'forge.enabledGameTestNamespaces', mod_id
            programArgument '--nogui'
        }

        data {
            data()
            gameDirectory = project.file('run/data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll('--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath())
        }
    }
    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
}

jar {
    manifest {
        attributes([
                "MixinConfigs": "${mod_id}.mixins.json"
        ])
    }
}

apply from: 'gradle/scripts/repositories.gradle'
apply from: 'gradle/scripts/dependencies.gradle'

processResources {
    var replaceProperties = [
            mod_id: mod_id,
            mod_name: mod_name,
            mod_version: mod_version,
            mod_description: mod_description,
            mod_authors: mod_authors,
            mod_credits: mod_credits,
            mod_url: mod_url,
            mod_update_json: mod_update_json,
            mod_logo_path: mod_logo_path,

            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range,

            forge_version: forge_version,
            forge_version_range: forge_version_range,
            loader_version_range: loader_version_range
    ]
    var filterList = [
            'META-INF/mods.toml',
            'pack.mcmeta'
    ]

    inputs.properties replaceProperties
    filesMatching(filterList) { fcd ->
        fcd.expand(replaceProperties + [project: project])
    }
}



// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.version,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

apply from: 'gradle/scripts/spotless.gradle'

// Rename the final file: delete regular *.jar (if produced) and strip the -all suffix from the shaded jar
tasks.register('renameAllJar') {
    doLast {
        def libsDir = file("${buildDir}/libs")
        if (!libsDir.exists()) return
        // Delete a possible regular jar first
        libsDir.listFiles()?.findAll { it.name == "${archivesBaseName}-${version}.jar" }?.each { File reg ->
            reg.delete()
        }
        // Rename *-all.jar to *.jar
        libsDir.listFiles()?.findAll { it.name.endsWith('-all.jar') }?.each { File f ->
            def targetName = f.name.replace('-all.jar', '.jar')
            def target = new File(f.parentFile, targetName)
            if (target.exists()) target.delete()
            if (!f.renameTo(target)) {
                throw new GradleException("Failed to rename ${f.name} to ${target.name}")
            }
        }
    }
}

// Run rename step after available packaging task (bind safely after tasks are created)
gradle.projectsEvaluated {
    tasks.findByName('reobfJarJar')?.finalizedBy('renameAllJar')
    tasks.findByName('reobfJar')?.finalizedBy('renameAllJar')
    // Fallbacks if reobf tasks are not present in this environment
    tasks.findByName('jarJar')?.finalizedBy('renameAllJar')
    tasks.findByName('jar')?.finalizedBy('renameAllJar')
}
