plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'com.diffplug.spotless' version '7.2.+'
    id 'io.freefair.lombok' version "6.6.+"
    id 'xyz.wagyourtail.unimined' version "1.4.+"
}


apply from: rootProject.file('gradle/scripts/helpers.gradle')


assertProperty 'mod_id'
assertProperty 'mod_name'
assertProperty 'mod_version'
assertProperty 'mod_package'

assertProperty 'minecraft_version'
assertProperty 'forge_version'
assertProperty 'java_version'

base {
    version = propertyString('mod_version')
    group = propertyString('mod_package')
    archivesName = propertyString('mod_name', 'mod_name_suffix')
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(
                propertyString('java_version') as int
        )
    }
}

configurations {
    embed
    contain
    implementation.extendsFrom(embed)
    implementation.extendsFrom(contain)
    modCompileOnly
    compileOnly.extendsFrom(modCompileOnly)
    modRuntimeOnly
    runtimeOnly.extendsFrom(modRuntimeOnly)
}


unimined.minecraft {
    version "1.20.1"

    mappings {

        intermediary()
        mojmap()
        parchment("2023.09.03")
        devNamespace("intermediary")
    }

    minecraftForge {
        if (propertyBool('use_access_transformer')) {
            accessTransformer "${rootProject.projectDir}/src/main/resources/META-INF/${propertyString('access_transformer_locations')}"
        }

        loader(propertyString('forge_version'))
        mixinConfig "${propertyString('mod_id')}.mixins.json"
    }


    defaultRemapJar = false

    remap(tasks.jar) {
        mixinRemap {
            enableBaseMixin()
            enableMixinExtra()
            disableRefmap()
        }
    }

    mods {
        remap(configurations.modCompileOnly)
        remap(configurations.modRuntimeOnly)
    }

}


apply from: rootProject.file('gradle/scripts/repositories.gradle')

dependencies {

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    annotationProcessor 'org.jetbrains:annotations:26.0.+'
    compileOnly 'org.jetbrains:annotations:26.0.+'

    annotationProcessor 'org.projectlombok:lombok:1.18.+'
    compileOnly 'org.projectlombok:lombok:1.18.+'

    if (propertyBool('enable_junit_testing')) {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
}
apply from: rootProject.file('gradle/scripts/dependencies.gradle')


processResources {
    var replaceProperties = [
            'mod_id'                 : propertyString('mod_id'),
            'mod_name'               : propertyString('mod_name'),
            'mod_version'            : propertyString('mod_version'),
            'mod_description'        : propertyString('mod_description'),
            'mod_authors'            : propertyString('mod_authors'),
            'mod_credits'            : propertyString('mod_credits'),
            'mod_url'                : propertyString('mod_url'),
            'mod_update_json'        : propertyString('mod_update_json'),
            'mod_logo_path'          : propertyString('mod_logo_path'),

            'minecraft_version'      : propertyString('minecraft_version'),
            'minecraft_version_range': propertyString('minecraft_version_range'),

            'forge_version'          : propertyString('forge_version'),
            'forge_version_range'    : propertyString('forge_version_range'),
            'loader_version_range'   : propertyString('loader_version_range')
    ]
    var filterList = [
            'META-INF/mods.toml',
            'pack.mcmeta'
    ]

    inputs.properties replaceProperties
    filesMatching(filterList) { fcd ->
        fcd.expand(replaceProperties + [project: project])
    }
}


jar {
    manifest {
        attributes([
                'Specification-Title'     : propertyString('mod_id'),
                'Specification-Vendor'    : propertyString('mod_authors'),
                'Specification-Version'   : '1',

                'Implementation-Title'    : propertyString('mod_name'),
                'Implementation-Version'  : propertyString('mod_version'),
                'Implementation-Vendor'   : propertyString('mod_authors'),
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy(tasks.named("remapJar"))
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}


apply from: rootProject.file('gradle/scripts/spotless.gradle')


//tasks.register('renameAllJar') {
//    doLast {
//        def libsDir = file("$buildDir/libs")
//        if (!libsDir.exists()) return
//
//        libsDir.listFiles()?.each { f ->
//            if (f.name.endsWith('-all.jar')) {
//                def target = new File(f.parentFile, f.name.replace('-all.jar', '.jar'))
//                if (target.exists()) target.delete()
//                f.renameTo(target)
//            }
//        }
//    }
//}
//
//gradle.projectsEvaluated {
//    tasks.findByName('reobfJarJar')?.finalizedBy('renameAllJar')
//    tasks.findByName('jarJar')?.finalizedBy('renameAllJar')
//
//    tasks.findByName('reobfJar')?.finalizedBy('renameAllJar')
//    tasks.findByName('jar')?.finalizedBy('renameAllJar')
//}
